apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group 'fr.varchar-dev'
version '1.0.9'

repositories {
    mavenCentral()
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier 'javadoc'
    from javadoc
}

artifacts {
    archives sourcesJar, javadocJar
}

dependencies {
    compile 'com.google.code.gson:gson:2.8.6'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact tasks.sourcesJar
            artifact tasks.javadocJar

            pom {
                groupId = project.group
                version = project.version
                artifactId = 'varlib'
                name = project.name
                description = 'java library'
                url = 'https://github.com/Pierre-development/VarLib'

                scm {
                    connection = 'scm:git:git://github.com/Pierre-development/VarLib.git'
                    developerConnection = 'scm:git:ssh://github.com:Pierre-development/VarLib.git'
                    url = 'https://github.com/Pierre-development/VarLib/master'
                }

                licenses {
                    license {
                        name = 'MIT license'
                        url = 'https://github.com/Pierre-development/VarLib/blob/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id = 'varchar'
                        name = 'VarChar'
                        email = 'sddslebg@gmail.com'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = project.version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
